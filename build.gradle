buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.codehaus.groovy:groovy-json:${GroovySystem.getVersion()}"
    }
}

plugins {
    id 'java'
}

tasks.register('generateEnums') {
    doLast {
        // input
        def enumDefinitionFile = new File("$projectDir/enums.json")
        def templateFile = new File("$projectDir/enum.template")
        // output
        def outputDir = "$projectDir/src/main/java/example"

        def templateEngine = new groovy.text.GStringTemplateEngine()
        def template = templateEngine.createTemplate templateFile.text
        def enums = new groovy.json.JsonSlurper().parseText enumDefinitionFile.text
        enums.each {
            def out = new File("${outputDir}/${it.name}.java")
            out.write template.make(it).toString()
            logger.info "Generated $out"
        }
    }
}